class:: MyPlucked
summary:: Plucked physical model with jawari.
categories:: UGens>PhysicalModels


Description::

Digital wave guide physical model of a plucked instrument.


Classmethods::

method::ar

argument::freq
Sound frequency.

argument::amp
Key pulsation strenght. Betwen 0 and 1.

argument::gate
Releases synth when value changes from >0 to 0.

argument::pos
Relative pluck position from 0 to 1.

argument::c1
Inverse of DC decay time.

argument::c3
Hight frequency loss factor.

argument::inp
Plucking signal. Could be first part of plucked sample for doing conmuted synthesis.

argument::release
Release time in seconds.

argument::jw
Jawari factor.


Examples::

code::
//Define pluck synthdef
(

~nbuf = 2048*4;
SynthDef(\reverb,{
    |out = 0, len = 2000, roomsize = #[6.2,8,2.7], receiver = #[3,3,1.2], b = 0.74, hw = 0.4, n = 3, revl = 0.3, bypass = 0, dist=1,angle=0.2|
    var in, bufL, bufR, trig, sigL, sigR, early, sig;
	var omangle = angle*pi*0.5;
	var source = [receiver[0] + (omangle.sin*dist), receiver[1] + (omangle.cos*dist), receiver[2]];
	in = Mix(In.ar(out,2))*0.5;
    bufL = LocalBuf(~nbuf);
    bufR = LocalBuf(~nbuf);
    trig = EarlyRefGen.kr(bufL, bufR, source, receiver, roomsize, hw, b.neg, n);
    sigL = PartConvT.ar(in, 2048, bufL, trig);
    sigR = PartConvT.ar(in, 2048, bufR, trig);
    early = [sigL, sigR];
    sig = DWGReverb3Band.ar(Mix(early) * 0.5, len) * revl + early;
    sig = Select.ar(bypass, [sig, in]);
    ReplaceOut.ar(out, sig);
}).add;

SynthDef(\plucksoundboard, {arg busin=0, busout=0,size=2.8,mix=0.8,bypass=0;
	var inp = In.ar(busin,2); 
    var dd = [199, 211, 223, 227, 229, 233, 239, 241]*size;
	var son = DWGSoundBoard.ar(Mix(inp),20,20,mix,dd[0],dd[1],dd[2],dd[3],dd[4],dd[5],dd[6],dd[7]);
    ReplaceOut.ar(busout,Select.ar(bypass,[son.dup,inp]));
}).add;

SynthDef(\help_dwgplucked, { |out=0, freq=440, amp=0.5, gate=1, c3=20, jw = 24, pan=0|
	var env = Env.new([0,1, 1, 0],[0.001,0.006, 0.0005],[5,-5, -8]);
	var inp = amp * LFClipNoise.ar(2000) * EnvGen.ar(env,gate);
    var son = MyPlucked.ar(freq, amp, gate,0.1,1,c3,inp,0.1,jw*0.0001);
    DetectSilence.ar(son, 0.001, doneAction:2);
    Out.ar(out, Pan2.ar(son * 0.1, pan));
}).add;
)

//Use it.
(
a = Scale.new(
    #[0, 1, 3, 5, 7, 8, 10],
    12,
    Tuning.new([0, 0.75, 2, 3.5, 4, 5, 6, 7, 8, 9, 10, 11]),
    "Custom"
);

b = Pbind(
    \instrument, \guitar,
    \scale ,a,
    \delta , Pseq([1,0.5,0.5,0.5,0.5,0.5,0.5,1,0.5,0.5,1,1]*0.25,inf),
    \dur , 2,
    \degree, Pseq([1,8,8,8,8,8,8,-6,Prand([9,10,11],1),1,7,8]-8, inf),
    \amp , Pwhite(0.1,0.5),
);

c = Pfx(b, \plucksoundboard);

d = Pfx(c, \reverb, \dist, 1);

d.play;
)
::
